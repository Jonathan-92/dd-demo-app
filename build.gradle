
plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

repositories {
    mavenLocal()
    mavenCentral()
}

version = '0.1.0'
mainClassName = 'org.dd.demoapp.App'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext {
    dropwizardVersion = "0.9.1"
    dropwizardModulesVersion = '0.9.0-1'
}

configurations {
    quasar
}

dependencies {
    compile "org.slf4j:slf4j-api:1.7.12"
    compile "co.paralleluniverse:quasar-core:0.7.3:jdk8"
    quasar "co.paralleluniverse:quasar-core:0.7.3:jdk8@jar"
    compile "co.paralleluniverse:comsat-dropwizard:0.5.0"
    compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
    compile "io.dropwizard:dropwizard-assets:$dropwizardVersion"
    compile "io.dropwizard.modules:dropwizard-java8:$dropwizardModulesVersion"

    testCompile "junit:junit:4.12"
    testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
}

shadowJar {
    mergeServiceFiles()
}

tasks.withType(Test) {
    jvmArgs "-javaagent:${configurations.quasar.singleFile}"
}

applicationDefaultJvmArgs = [
        "-javaagent:${configurations.quasar.singleFile}"
]

//System property to verify that @Suspend and throw SuspendExecution is properly set wherever they are needed
//co.paralleluniverse.fibers.verifyInstrumentation=true

run {
    args 'server', 'app-config.yml'
}

